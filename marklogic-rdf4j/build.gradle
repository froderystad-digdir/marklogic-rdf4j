buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven{ url "https://plugins.gradle.org/m2/"}
    }
    dependencies {
        classpath "gradle.plugin.com.marklogic:ml-gradle:3.0.0"
    }
}

apply plugin: "com.marklogic.ml-gradle"
//apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'jacoco'
jacoco{toolVersion="0.7.4.201502262128"}

compileJava.options.encoding = 'UTF-8'

configure(allprojects){
            ext.slf4jVersion = '1.7.25'
            ext.logbackVersion = '1.2.3'
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
}

jacocoTestReport {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
}

dependencies {
    compile('org.eclipse.rdf4j:rdf4j-runtime:2.4.3')

    compile('com.marklogic:marklogic-client-api:5.4.0') {
        exclude(group: 'org.slf4j')
        exclude(group: 'ch.qos.logback')
        exclude(group: 'javax.ws.rs')
    }
    compile "javax.ws.rs:javax.ws.rs-api:2.1@jar"
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "ch.qos.logback:logback-classic:$logbackVersion"
    compile "org.slf4j:jcl-over-slf4j:$slf4jVersion"

    testCompile 'com.jayway.restassured:rest-assured:2.4.1'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'junit:junit:4.12'
}

sourceSets {
    test {
        java {
            srcDir 'main/test/java'
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.5.1'
}

javadoc {
    options.overview = "src/main/java/com/marklogic/semantics/rdf4j/overview.html"
}

Node pomCustomizations = new NodeBuilder(). project {
        name 'marklogic-rdf4j'
        packaging 'jar'
        textdescription 'Adapter for using MarkLogic with the RDF4J Framework'
        url 'https://github.com/marklogic/marklogic-rdf4j'

        scm {
            url 'git@github.com:marklogic/marklogic-rdf4j.git'
                connection 'scm:git@github.com:marklogic/marklogic-rdf4j.git'
                developerConnection 'scm:git@github.com:marklogic/marklogic-rdf4j.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                name 'MarkLogic'
                    email 'java-sig@marklogic.com'
                    organization 'MarkLogic'
                    organizationUrl 'https://www.marklogic.com'
            }
            developer {
                name 'MarkLogic Github Contributors'
                    email 'general@developer.marklogic.com'
                    organization 'Github Contributors'
                    organizationUrl 'https://github.com/marklogic/marklogic-rdf4j/graphs/contributors'
            }
        }
}

publishing {
  publications {
    mainJava(MavenPublication) {
      from components.java

      pom.withXml {
        asNode().append(pomCustomizations.developers)
        asNode().append(pomCustomizations.name)
        asNode().append(pomCustomizations.packaging)
        asNode().append(pomCustomizations.url)
        asNode().append(pomCustomizations.scm)
        asNode().append(pomCustomizations.licenses)
        asNode().appendNode("description",pomCustomizations.textdescription.text())
      }

      artifact sourcesJar

      artifact javadocJar

    }
  }
  repositories {
    maven {
      if(project.hasProperty("mavenUser")) {
          credentials {
              username mavenUser
              password mavenPassword
          }
      }
      url publishUrl
    }
  }
}
